Julia采用（ships with）预定义的类型表达复数和有理数（分数），支持全部标准算术操作符和基本函数。
还规定了【转换和提升】以确保任何预定义数值类型组合（原始类型或复合类型）如期操作。

# 复数数字

全局常量`im`绑定为负数数字`i`，表示`-1`的“主平方根”。
指派`i`作为全局常量被认为是有害的，因为`i`是如此常用的变量名。
由于Julia允许数值字面为“【[并置标识符作系数](./整型数字和浮点型数字.md "Integers and Floating-Point Numbers")】”，这样绑定`im`足以提供复数数字的实用语法，类似传统数学注解。

```
julia> x = 0 + 1im
0 + 1im

julia> typeof(x)
Complex{Int64}
```

可以在复数数字执行全部标准算术操作：

```
# 接着上述示例代码做
julia> x * y
-2 + 2im

julia> x / y
0.25 + 0.25im

julia> x // y
1//4 + 1//4*im

julia> x \ y
2.0 - 2.0im

julia> x^2
-1 + 0im

julia> y^2.50
-5.148754023244662 + 12.4301917922403im

julia> x .* [0 1 2 3 4]
1×5 Array{Complex{Int64},2}:
 0+0im  0+1im  0+2im  0+3im  0+4im
```

类型**转换和提升**保证不同类型操作数组合也正常工作：

```
julia> 3(1 - 1im)
3 - 3im

julia> 3/(1 - 1im)
1.5 + 1.5im

# 因为字面系数优先级高于除操作
# 解释为“3/(4im) == -(3/4*im)”
julia> 3/4im
0.0 - 0.75im
```

提供了巧妙处理复数的标准函数：

```
julia> z = 9527 + 1314im
9527 + 1314im

julia> real(z)
9527

julia> imag(z)
1314

julia> conj(z)
9527 - 1314im

julia> abs(z)
9617.189038383305

julia> abs2(z)
92490325

julia> angle(z)
0.13705907049863766

julia> sqrt(1im)
0.7071067811865476 + 0.7071067811865475im

julia> cos(1 + 2im)
2.0327230070196656 - 3.0518977991518im

julia> exp(1 + 2im)
-1.1312043837568135 + 2.4717266720048188im
```

像通常一样，复数的绝对值（abs）是到零的距离；而`abs2`是绝对值的平方，特别适用于避免取平方根的场合。
弧度（angle）返回复数描述的相角（也是`arg`函数的参数）。
其余基本函数对复数数字也是“全（gamut）覆盖”。

注意：数学函数一向处理实数返回实数、处理复数返回复数。

```
# 实数域内不持支负数开平方
julia> sqrt(-1)
ERROR: DomainError with -1.0:
sqrt will only return a complex result if called with a complex argument. Try sqrt(Complex(x)).
Stacktrace:
 [1] throw_complex_domainerror(::Symbol, ::Float64) at .\math.jl:31
 [2] sqrt at .\math.jl:479 [inlined]
 [3] sqrt(::Int64) at .\math.jl:505
 [4] top-level scope at none:0

# 复数域内的特色就是支持负数开平方
julia> sqrt(-1 + 0im)
0.0 + 1.0im
```

数值字面系数注解不支持直接将变量和`im`结合，必须显式写出“*”。
```
julia> a, b = 1, 2
(1, 2)

julia> c = a + b*im
1 + 2im

julia> c = a + bim
ERROR: UndefVarError: bim not defined
Stacktrace:
 [1] top-level scope at none:0
```

不推荐用字面系数来构建复数，最好用内建`complex`函数通过实部、虚部来构建复数。
```
# 接着上述示例代码做
julia> complex(a, b)
1 + 2im
```
这样可避免“乘加操作”。

如【[特殊浮点值](./整型数字和浮点型数字.md "Integers and Floating-Point Numbers")】所描述的，无穷（Inf）和非数值（NaN）也能出现在复数的实部或虚部。
```
julia> Inf + Inf*im
Inf + Inf*im

julia> NaN + NaN*im
NaN + NaN*im
```

# 有理数（分数）

Julia的有理数类型用来精确表达整数的比率（分数）。
有理数用整除符号（`//`）来构建：
```
julia> 3 // 7
3//7
```

如果分数操作数（分子分母）有公约数，则分解为最小项且分母非负：
```
julia> 3 // -15
-1//5
```

正因如此，整数比率（分数）的正规化形式是唯一的，就可以通过测试分子分母相等否来判断有理数相同否。
还有`numerator`和`denominator`函数来抽取有理数的分子和分目。
```
julia> numerator(2//3)
2

julia> numerator(4//6)
2

julia> denominator(2//3)
3

julia> denominator(4//6)
3
```

当然，直接比较分子分目没有必要，标准算术和比较操作也适用于有理数。
```
julia> 2//3 == 6//9
true

julia> 2//3 == 16//27
false

julia> 2//3 > 16//27
true

julia> 2//3 + 16//27
34//27

julia> 2//3 - 16//27
2//27

julia> 2//3 * 16//27
32//81

julia> 2//3 / 16//27
9//8

julia> 1314 // 9527
1314//9527

julia> lcm(1314, 9527)
12518478

julia> gcd(1314, 9527)
1
```

*关于9527追求1314的情况，见秀才知乎[帖子](https://www.zhihu.com/question/264640747)。*
```
# Python
>>> for x in range(1,9528):
...   if 9527 % x == 0:
...     print x
...
1
7
1361
9527

>>> for x in range(1,1315):
...   if 1314 % x == 0:
...     print x
...
1
2
3
6
9
18
73
146
219
438
657
1314
```

有理数转换成浮点型数字（So Easy）：
```
julia> float(3//4)
0.75

julia> float(1314//9527)
0.13792379552849796
```

从有理数转换为浮点型数字，遵守如下恒等式（分子分目为零除外）：
```
julia> a = 1; b = 2
2

julia> isequal(float(a//b), a/b)
true

julia> 0 // 0
ERROR: ArgumentError: invalid rational: zero(Int64)//zero(Int64)
Stacktrace:
 [1] Type at .\rational.jl:13 [inlined]
 [2] Type at .\rational.jl:18 [inlined]
 [3] //(::Int64, ::Int64) at .\rational.jl:41
 [4] top-level scope at none:0

julia> 0 / 0
NaN

julia> 1 // 0
1//0

julia> 1 / 0
Inf
```

可以接受无穷分数（分母为零且分子不为零）；不接受NaN有理数（分母分子同时为零）。
```
# 分子分目必须为整数（多此一举——有理数（分数）的数学定义内在要求如此）
julia> 0.1 // 1
ERROR: MethodError: no method matching //(::Float64, ::Int64)
Closest candidates are:
  //(::Integer, ::Integer) at rational.jl:41
  //(::Rational, ::Integer) at rational.jl:44
  //(::Complex, ::Real) at rational.jl:57
  ...
Stacktrace:
 [1] top-level scope at none:0

julia> 1 // 1.0
ERROR: MethodError: no method matching //(::Int64, ::Float64)
Closest candidates are:
  //(::Integer, ::Integer) at rational.jl:41
  //(::Integer, ::Rational) at rational.jl:48
  //(::Complex, ::Real) at rational.jl:57
  ...
Stacktrace:
 [1] top-level scope at none:0
```

照例，转换和提升系统让有理数和别的数值类型交互不费吹灰之力（effortless）。
```
julia> 1314//9527 + 9527 - 1314
78246565//9527

julia> 1314//9527 + 9527.1314
9527.269323795528

julia> 1314//9527 + (9527 + 1314im)
90765043//9527 + 1314//1*im
```

---
# 译后感

- Python把有理数放入了标准库（v2.6+）。
