构造函数就是创建新对象的函数，特别是【[复合类型](./类型.md "Types")】实例。
在Julia中，类型对象也可作构造函数：当按照函数调用那样应用参数，创建新的自身类型实例。
之前在介绍复合类型时已经大量提及。例如：
```
julia> struct Fuck
         alice
         bob
       end

julia> fuck = Fuck("qiuxiang", "huaan")
Fuck("qiuxiang", "huaan")

julia> fuck.alice
"qiuxiang"

julia> fuck.bob
"huaan"
```
对很多类型来说，形成新的对象，无外乎将字段值绑定在一起。
然而，创建复合对象，需要更多功能。
有些时候，不变量必须强制执行，要么检查参数、要么转换参数。
[递归数据结构](https://en.wikipedia.org/wiki/Recursion_%28computer_science%29#Recursive_data_structures_.28structural_recursion.29)，特别是需要引用自身的，
通常在未完成状态下，*不借助首先创建的、不能清晰构建*，并且随后程序化改变即成整体，是不同于对象创建的步骤。
有些时候，构造带少数不同参数的对象比有字段的便捷些。
Julia的对象构造系统处理的不止于此。

术语（nomenclature）：因为构造函数一说参考全部构造某种类型对象的函数，轻微地滥用术语并引用特定构造方法为构造函数是稀松平常的。
在这种情况下，一般明确表达“构造方法”概念而不是“构造函数”术语，特别是经常用在将所有别的方法和特殊的构造方法区分开上。

# 外部构造方法

构造方法仅如Julia中别的函数一样，它的全部即是方法绑定的行为。
相应地，可以简单定义新的方法给构造函数添加功能。
例如，为`Fuck`对象添加只接受一个参数、参数值传递给`alice`和`bob`字段的构造方法（自操）。看代码：
```
julia> Fuck(who) = Fuck(who, who)
Fuck

julia> selfuck = Fuck("cto", "cto")
Fuck("cto", "cto")
```
可以添加无参数的`Fuck`构造方法（空操），为`alice`和`bob`提供了默认值。
```
julia> Fuck() = Fuck("hr")
Fuck

julia> Fuck()
Fuck("hr", "hr")
```
空操屌自操。
依次屌自动提供的双参数构造方法。
种种原因以至清晰简短，额外的构造方法如正常方法一样声明，称作“外部构造方法”。
外部构造方法仅可通过调用别的构造方法来创建新实例，例如自动提供的默认构造方法。

# 内部构造方法

虽然外部构造方法在处理提供额外便捷构造对象的方法问题上大获全胜，但在处理本文提到的另外两种情况下一败涂地：
强制不变量、允许构建自引用的对象。
针对这些问题，需要内部构造方法。
内部构造方法和外部构造方法很像，但有两点不同：

- 在类型声明内声明，而不是像正常方法那样在类型声明外声明。
- 可访问本地已有特殊函数`new`来创建块类型的对象。

举个栗子，假定声明持有一对实数的类型，受“第一个数不能大于第二个数”的限制。
可如下撸码：
```
julia> struct OrderedPair
         prefix::Real
         postfix::Real
         OrderedPair(prefix, postfix) = prefix > postfix ? error("out of order") : new(prefix, postfix)
       end
```
现在`OrderedPair`对象只能以`prefix <= postfix`构造：
```
julia> OrderedPair(0, 1)
OrderedPair(0, 1)

julia> OrderedPair(9527, 1314)
ERROR: out of order
Stacktrace:
 [1] error(::String) at .\error.jl:33
 [2] OrderedPair(::Int64, ::Int64) at .\REPL[9]:4
 [3] top-level scope at none:0
```
如果类型声明`mutable`则进而直接修改字段值更改该不变量，但对象内部未邀请的混乱被认为是垃圾形式。
可在之后任意位置提供额外的外部构造方法，但类型一经声明，没有添加别的内部构造方法的门儿。
由于外部构造方法只能通过调用别的构造方法来创建对象，最终，某些内部构造方法必被调用来创建实例。
这保证所有已声明的类型之对象必须经调用类型提供的某个内部构造方法而存在，从而提供某种程度的类型不变性强制实行。

如果任何内部构造方法已被定义，却没有提供默认构造方法：假定已经备好所需的一切内部构造方法。
默认构造方法和编写带全部对象字段为参数的内部构造方法等价，当然必须是正确的类型，如果对应的字段有类型限制的话，并将这些参数传递给`new`函数，得到新实例结果。
```
julia> struct Kiss
         qiuxiang
         huaan
         Kiss(qx, ha) = new(qx, ha)
       end
```
如此声明和前边的`Fuck`不带显式默认构造方法效果一样。
下述两个类型等价，一个是自动默认构造方法（隐式）、一个是显式默认构造方法。
```
julia> struct T
         t::Int
       end

julia> struct TT
         t::Int
         TT(t) = new(t)
       end

julia> T == TT
false

julia> t = T(9527)
T(9527)

julia> tt = TT(9527)
TT(9527)

julia> t == tt
false
```
*搞清楚，是等价类型，不是相同类型。*
尽可能提供少地提供内部构造方法被认为是好的形式：仅这些内部构造方法显式处理所有参数并强制检查基本错误及转换。
别的便捷构造方法，提供默认值或辅助转换，应当以外部构造方法提供，继而调用内部构造方法处理繁重事务。
这种隔离是（典型地）十分自然。

# 不完全初始化

最后的问题，仍然未被处理的是自引用对象的构造，或更一般地说，是递归数据结构。
由于根本困难可能不会一下子明晰起来，一起简明扼要地解释下。
考虑下面递归类型声明：
```
julia> mutable struct SelfReferential
         o::SelfReferential
       end
```
该类型显得足够无伤大雅，止于考虑如何构造它的实例。
如果`alice`是`SelfReferential`的一个实例，则下一个实例`bob`可以这样创建：
```
bob = SelfReferential(alice)
```
但是，没有已存在的、可以作为有效值提供给`SelfReferential`字段`o`的实例，如何构建首个`SelfReferential`实例？
*先有鸡还是先有蛋？*
唯一的解决办法就是允许创建一个不完全初始化的`SelfReferential`实例，其`o`字段未赋值，将此不完全初始化实例作为后续实例创建时的`o`字段有效值。

允许创建不完全初始化对象，Julia允许`new`函数用少于某类型应有字段个数的参数调用之，返回未指定字段未初始化的对象。
内部构造方法接着可用未完全初始化对象，在返回之前完成初始化。
这里，举个例子，再试下别的，在定义`SelfReferential`类型时，内部无参数构造方法返回包含指向自身的`o`字段的实例。
```
mutable struct SelfReferential
    o::SelfReferential
    SelfReferential() = (incomplete = new(); incomplete.o = incomplete)
end

alice = SelfReferential()

println(alice === alice)
println(alice === alice.o)
println(alice === alice.o.o)
```
如上边代码，已经验证构造方法有效且构造出的对象实际上是自引用的。

尽管从内部构造方法返回完全初始化的对象通常是个好主意，可以不完全初始化的对象。
```
julia> mutable struct Incomplete
         sb
         Incomplete() = new()
       end

julia> cto = Incomplete()
Incomplete(#undef)
```
虽然允许创建带未初始化字段的对象，但任何对未初始化引用的访问都直接报错：
```
julia> cto.sb
ERROR: UndefRefError: access to undefined reference
Stacktrace:
 [1] getproperty(::Any, ::Symbol) at .\sysimg.jl:18
```
这避免不停检查`null`值。
然而，并非所有对象字段都是引用。
Julia认为某些类型是“纯数据”，意味着其中所有数据都是自包含的、不引用别的对象。
纯数据由原始类型（如`Int`等）和别的纯数据类型的不可变结构体组成。
纯数据类型的初始化内容是未定义的。
```
julia> struct HasPlain
         n::Int
         HasPlain() = new()
       end

julia> HasPlain()
HasPlain(689197664)
```
纯数据类型数组展现同样的行为。

可从内部构造方法传递不完备对象给别的函数，委托其完成初始化：
```
mutable struct Lazy
    sb
    Lazy(name) = completeit(new(), name)
end

function completeit(o::Lazy, name::AbstractString)
    o.sb = name
    return o
end

cto = Lazy("cto")
println(cto)
println(cto.sb)
```
正如构造方法返回不完备的对象一样，如果`completeit`函数或任何它的被调用对象尝试访问`Lazy`对象的`sb`字段——在该字段被初始化之前——马上错误伺候。

# 带参构造方法

带参类型给构造方法故事平添几分波折（wrinkles）。
回想【[参数类型](./类型.md "Types - Parametric")】中，默认地，带参复合类型可以显式提供参数类型，也可以参数值类型隐式提供。举些例子：
```
julia> struct Point{T<:Real}
         x::T
         y::T
         z::T
       end

julia> Point(0, 1, 250)
Point{Int64}(0, 1, 250)

julia> Point(2, 9527, 1314.0)
ERROR: MethodError: no method matching Point(::Int64, ::Int64, ::Float64)
Closest candidates are:
  Point(::T<:Real, ::T<:Real, ::T<:Real) where T<:Real at REPL[16]:2
Stacktrace:
 [1] top-level scope at none:0

julia> Point{Float64}(1.0, 1.0, 1.0)
Point{Float64}(1.0, 1.0, 1.0)

julia> Point{Float64}(1, 1, 1)
Point{Float64}(1.0, 1.0, 1.0)

julia> Point{Int64}(2.5, 2.5, 2.5)
ERROR: InexactError: Int64(Int64, 2.5)
Stacktrace:
 [1] Type at .\float.jl:700 [inlined]
 [2] convert at .\number.jl:7 [inlined]
 [3] Point{Int64}(::Float64, ::Float64, ::Float64) at .\REPL[16]:2
 [4] top-level scope at none:0
```
如君所见，对于显式类型参数的构造方法调用，参数转换为隐含字段类型：`Point{Float64}(1, 1, 1)`有效，但`Point{Int64}(2.5, 2.5, 2.5)`报`InexactError`错误（转换`2.5`为`Int64`时）。

**注意`Point{Int64}(1.0, 1.0, 1.0)`是没有问题的。**

当类型由参数值隐含地传递给构造方法调用，如`Point(0, 1, 250)`，则参数类型必须赞成，否则`T`不能被检测到，但任何类型匹配的实数参数组都给到一般的`Point`构造方法。

这究竟怎么一回事，是`Point`、`Point{Float64}`、`Point{Int64}`均为不同狗制造函数。
实际上`Point{T}`对于每种`T`是完全不同的构造函数。
没有显式提供任何内部构造方法，复合类型`Point{T<:Real}`声明自动提供一个内部构造方法`Point{T}`，对于每种可能的`T<:Real`类型，行为只若无参数的默认内部构造方法。
它还提供了单个普通外部构造方法`Point`，带三个实数参数，三个参数值类型必须相同。
自动提供的构造方法和下面显式声明的等价：
```
julia> struct Point{T<:Real}
         x::T
         y::T
         z::T
         Point{T}(x,y,z) where {T<:Real} = new(x,y,z)
       end

julia> Point(x::T, y::T, z::T) where {T<:Real} = Point{T}(x,y,z)
Point
```
注意，每个定义看起来像其所处理的构造方法调用的形式。
调用`Point{Int64)(1.0,1.0,1.0)`将会调用`Point{T}(x,y,z)`这个`struct`块内的构造方法。
外部构造方法声明，另一方面，定义了普通`Point`构造方法，仅适用三个相同类型参数。
该声明让构造函数调用无显式类型参数，如`Point(1,1,1)`和`Point(2,2,2)`都有效。
因为方法声明限制参数必须是相同类型，如`Point(1,1,1.0)`的调用，参数类型不同，导致“无方法”错误。

假定大家想要造`Point(1,1,2.5)`有效构造方法，通过提升整数值`1`为浮点值`1.0`。
最简单的方式是定义额外的外部构造方法：
```
julia> Point(x::Int64, y::Int64, z::Float64) = Point(convert(Float64,x),convert(Float64,y),z)
Point

julia> Point(1,1,2.5)
Point{Float64}(1.0, 1.0, 2.5)

julia> typeof(ans)
Point{Float64}
```
该方法采用`convert`函数显式转换`x`和`y`为`Float64`然后委派构造方法为所有参数均为`Float64`类型情况对应的的一般构造方法。
借助该方法定义，前者`MethodError`现在可成功创建`Point{Float64}`类型的点。

然而，别的近似调用仍然无效：
```
julia> Point(1.0,1,2.5)
ERROR: MethodError: no method matching Point(::Float64, ::Int64, ::Float64)
Closest candidates are:
  Point(::Int64, ::Int64, ::Float64) at REPL[3]:1
  Point(::T<:Real, ::T<:Real, ::T<:Real) where T<:Real at REPL[2]:1
Stacktrace:
 [1] top-level scope at none:0
```
让这样的调用聪明地有效之更多一般方法，参考【[转换和提升](./转换和提升.md "Conversion and Promotion")】。
冒着破坏悬念地风险（at the risk of spoiling the suspense），咱可在此透露，可用下边外部构造方法将所有一般`Point`构造方法如调用者预期的有效。
```
julia> Point(x::Real, y::Real, z::Real) = Point(promote(x,y,z)...)
Point
```
此`promote`函数抓换所有它的参数为共同类型，本例中是`Float64`。
借助该方法，`Point`构造方法如数值操作（像`+`等）那样提升参数并对所有实数值有效。
```
julia> Point(3/4,1.0,1314//9527)
Point{Float64}(0.75, 1.0, 0.13792379552849796)

julia> Point(0+1im,2-2.5im,9527.0+1314im)
ERROR: MethodError: no method matching Point(::Complex{Int64}, ::Complex{Float64}, ::Complex{Float64})
Stacktrace:
 [1] top-level scope at none:0
```
这样，尽管Julia默认提供的隐式类型参数构造方法相当严格，但是制造更放松且明智的方法可行、易行。
此外，由于构造方法可做类型系统全部能力的杠杆，多重载，定义复杂行为，非常简单。

# 用例学习：有理数

可能把本文这些碎片绑定在一起的最好的方式是表达现实世界带参数复合类型及其构造方法的例子。
为了那个目的（to that end），实现自己的有理数类型`OurRational`，与Julia内建的`Rational`（定义在[`rational.jl`](https://github.com/JuliaLang/julia/blob/master/base/rational.jl)中）近似。
```
struct OurRational{T<:Integer} <: Real # 声明`OurRational`类型带一个某种`Integer`类型的类型参数（其本身是`Real`类型）
    numerator::T # 分子是某种`Integer`类型
    denominator::T # 分母是某种`Integer`类型
    # 现在事情变得有趣起来
    # 只有单个内部构造方法检查所有参数非零
    function OurRational{T}(n::T, d::T) where T<:Integer
        if n == 0 && d == 0
            error("invalid rational: 0//0")
        end
        # 保证每个有理数满足正分母之底线（lowest term）构造
        # 求分子分母最大公约数
        # 最大公约数的符号同`gcd`的首个参数
        g = gcd(d, n)
        # 分子分母分别除以两者的最大公约数
        n = div(n, g)
        # 因此新`d`总是非负
        d = div(d, g)
        # 由于仅此一个内部构造方法
        # 确保`OurRational`实例总是此正规形式
        new(n, d)
    end
end

# 简便起见还提供若干外部构造方法

# 分子分母必须是相同类型`T`（某种`Integer`类型）
OurRational(n::T, d::T) where {T<:Integer} = OurRational{T}(n, d)

# 分子分母类型不同
# 则提升为相同类型
# 而后委派匹配方法
OurRational(n::Integer, d::Integer) = OurRational(promote(n, d)...)

# 分子必须`Integer`类型
# 分母默认为一
OurRational(n::Integer) = OurRational(n, one(n))

# 为`⊘`操作符定义些许方法
# 操作语法是`left ⊘ right`
# 大家知道Julia官方`Rational`通过`//`操作符达此目的
# 在下述定义之前`⊘`是未定义无意义的语法符号
# 而后则等同【复数和有理数】的行为
# 全部动作寥寥数行

# 让`n⊘d`构造`OurRational`且`n`和`d`必须是`Integer`类型
# 此时一旦编写`left⊘right`即`OurRational`矣
# 比例结果略有不同
# 实则整数除法耳
⊘(n::Integer, d::Integer) = OurRational(n, d)

⊘(or::OurRational, k::Integer) = or.numerator ⊘ (or.denominator*k)

⊘(k::Integer, or::OurRational) = (k*or.denominator) ⊘ or.numerator

⊘(k::Complex, m::Real) = complex(real(k) ⊘ m, imag(k) ⊘ m)

⊘(k::Real, m::Complex) = (k*m') ⊘ real(m*m')

# 最后将`⊘`应用于复数整体
# 构成`Complex{OurRational`实例
# 复数的实数部分和虚数部分尽皆有理数
function ⊘(p::Complex, q::Complex)
    pq = p*q'
    qq = real(q*q')
    complex(real(pq) ⊘ qq, imag(pq) ⊘ qq)
end

sb = (1+2im) ⊘ (1-2im)
println(typeof(sb)) # Complex{OurRational{Int64}}

println(typeof(sb) <: Complex{OurRational}) # false
```
因此，尽管`⊘`操作符通常返回`OurRational`实例，如果所有参数是复整数，将返回`Complex{OurRational}`实例。
感兴趣的朋友可以详查`rational.jl`的剩余部分：简单、自包含，实现完整基本Julia类型。

# 仅外部构造方法

如所看到的，典型的带参数类型有内部构造方法，在已知类型参数时被调用。
例如应用`Point{Int}`而非`Point`。
随意地，可添加外部自动检测类型参数的构造方法，例如调用`Point(0,1,2)`构造`Point{Int}`实例。
外部构造方法调用内部构造方法干创建实例的核心工作。
然而，在某些场合，可以不提供内部构造方法，不能手动请求指定类型参数。

举个例子，定义保存自身精确求和结果的向量类型：
```
julia> struct SummedArray{T<:Number,S<:Number}
         data::Vector{T}
         sum::S
       end

julia> SummedArray(Int32[1;2;3], Int32(6))
SummedArray{Int32,Int32}(Int32[1, 2, 3], 6)
```
问题是想要`S`类型比`T`类型“大”，这样可以求和更多元素减少信息损失。
例如，当`T`是`Int32`，可以让`S`是`Int64`。
因此，想要避免允许用户构造`SummedArray{Int32,Int32}`类型实例的接口。
一种方法是只提供`SummedArray`的构造方法，但要在`struct`定义体内一直默认构造方法的生成：
```
struct SummedArray{T<:Number,S<:Number}
    data::Vector{T}
    sum::S
    function SummedArray(v::Vector{T}) where T
        S = widen(T)
        new{T,S}(v, sum(S, v))
    end
end

SummedArray(Int32[1,2,3])
SummedArray(Int32[1,2,3], Int64(6)) # MethodError
SummedArray(Int32[1,2,3], Int32(6)) # MethodError
```
该构造方法将以`SummedArray(TypeNameArray)`语法调用。
语法`new{T,S}`允许指定构建方法的类型参数，例如，它将返回`SummedArray{T,S}`实例。
任何构造方法定义中均可用`new{T,S}`，但简便起见，到`new{}`的参数自动从已经构建好的类型中派生，如果可能的话。

---
# 译后感

- 的确，在此称为构造方法更贴切。
